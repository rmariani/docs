---
navhome: /docs/
---


# 2h: set logic

### `++in`

Set operations

Core whose arms contain a variety of functions that operate on `++set`s. Its sample accepts the input `++set` to be manipulated.

Accepts
-------

A `++set`.

Source
------

  ++  in                                                  ::  set engine
    ~/  %in
    |_  a/(tree)  :: (set)

Examples
--------

    > ~(. in (sy "asd"))                                  ::  `++sy` turns a noun into a set
    <16.rzi {a/?({?(n/#1 n/@tD) l/nlr(?(#1 @tD)) r/nlr(?(^#1.?(#1 @tD) @tD))} $~) <119.wpb 31.ohr 1.jmk $143>}>

### `+-all:in`

Logical AND

Computes the logical AND on every element in `a` slammed with `b`.
That is, only if every element in set `a` satisfies the given
condition will a `%y` boolean be produced.

(To "slam" means to call a gate and give it a sample/samples.
In this instance, `a` is the set of samples that are given to
the gate `b`.)

Accepts
-------

`a` is a `++set`.

`b` is a wet gate that accepts a noun and produces a boolean.

Produces
--------

A boolean.

Source
------

      +-  all                                               ::  logical AND
        ~/  %all
        |*  b/$-(* ?)
        |-  ^-  ?
        ?~  a
          &
        ?&((b n.a) $(a l.a) $(a r.a))


Examples
--------

    > =a (sy `(list [@t *])`[['a' 1] ['b' [2 3]] ~])        ::  `++sy` turns a noun into a set
    > (~(all in a) |=(b/* ?@(+.b & |)))                     
        %.n
    > =a (sy `(list @t)`['john' 'bonita' 'daniel' 'madeleine' ~])
    > (~(all in a) |=(b/@t (gte b 100)))
        %.y

------------------------------------------------------------------------

### `+-any:in`

Logical OR

Computes the logical OR on every element of `a` slammed with `b`.
That is, if at least one element in set `a` satisfies the given
condition, then a `%y` boolean will be produced.

(To "slam" means to call a gate and give it a sample/samples.
In this instance, `a` is the set of samples that are given to
the gate `b`.)

Accepts
-------

`a` is a `++set`.

`b` is a gate that accepts a noun and produces a boolean.

Produces
--------

A boolean.

Source
------

      +-  any                                               ::  logical OR
        ~/  %any
        |*  b/$-(* ?)
        |-  ^-  ?
        ?~  a
          |
        ?|((b n.a) $(a l.a) $(a r.a))
      ::


Examples
--------

    > =a (sy `(list [@t *])`[['a' 1] ['b' [2 3]] ~])       ::  `++sy` turns a noun into a set
    > (~(any in a) |=(b/* ?@(+.b & |)))                   
        %.y
    > =a (sy `(list @t)`['john' 'bonita' 'daniel' 'madeleine' ~])
    > (~(any in a) |=(b/@t (lte b 100)))
        %.n

------------------------------------------------------------------------

### `+-del:in`

Remove noun

Removes `b` from the `++set` `a`.

Accepts
-------

`a` is a set.

`b` is a noun.

Produces
--------

A set.

Source
------

      +-  del                                               ::  b without any a
        ~/  %del
        |*  b/*
        |-  ^+  a
        ?~  a
          ~
        ?.  =(b n.a)
          ?:  (hor b n.a)
            [n.a $(a l.a) r.a]
          [n.a l.a $(a r.a)]
        |-  ^-  [$?($~ _a)]
        ?~  l.a  r.a
        ?~  r.a  l.a
        ?:  (vor n.l.a n.r.a)
          [n.l.a l.l.a $(l.a r.l.a)]
        [n.r.a $(r.a l.r.a) r.r.a]
      ::


Examples
--------

    > =a (sy `(list @t)`['a' 'b' 'c' ~])                   ::  `++sy` turns a noun into a set
    > (~(del in a) 'b')
    [n='c' l=[n='a' l={} r={}] r=~]
    > `(set @t)`(~(del in a) 'b')
    {'a' 'c'}
    > =a (sy `(list @t)`['john' 'bonita' 'daniel' 'madeleine' ~])
    > `(set @t)`(~(del in a) 'john')
    {'daniel' 'bonita' 'madeleine'}
    > `(set @t)`(~(del in a) 'susan')
    [n='madeleine' l=[n='daniel' l={} r={'bonita'}] r=[n='john' l={} r={}]]

------------------------------------------------------------------------

### `+-dig:in`

Axis a in b

Produce the axis of `b` within `a`. An "axis" is an address
in a noun's tree. Check out the Nock [explanation](docs/nock/explanation/) for more
information on the tree addressing system.

Accepts
-------

`a` is a `++set`.

`b` is a noun.

Produces
--------

The `++unit` of an atom.

Source
------

      +-  dig                                               ::  axis of a in b
        |=  b/*
        =+  c=1
        |-  ^-  (unit @)
        ?~  a  ~
        ?:  =(b n.a)  [~ u=(peg c 2)]
        ?:  (hor b n.a)
          $(a l.a, c (peg c 6))
        $(a r.a, c (peg c 7))
      ::



Examples
--------

    > =a (sy `(list @)`[1 2 3 4 5 6 7 ~])                  ::  `++sy` turns a noun into a set
    > a
    [n=6 l={5 4 7} r={1 3 2}]
    > -.a
    n=6
    > (~(dig in a) 7)
    [~ u=12]
    > (~(dig in a) 2)
    [~ u=14]
    > (~(dig in a) 6)
    [~ u=2]
    > (~(dig in a) 8)
    ~

------------------------------------------------------------------------

### `+-gas:in`

Concatenate

Insert the elements of a `++list` `b` into a `++set` `a`.

Accepts
-------

`a` is a set.

`b` is a list.

Produces
--------

A `++set`.

Source
------

      +-  gas                                               ::  concatenate
        ~/  %gas
        |=  b/(list _?>(?=(^ a) n.a))
        |-  ^+  a
        ?~  b
          a
        $(b t.b, a (put i.b))
      ::


Examples
--------
    > =a (sy `(list @t)`['john' 'bonita' 'rudolf' 'madeleine' ~])          ::  `++sy` turns a noun into a set
    > (~(gas in a) ['well' 'things' 'number' 'leet' ~])
    [n='number' l=[n='14' l={'1.337'} r={}] r=[n='madeleine' l={'things' 'bonita'} r={'rudolf' 'john'}]]
    > `(set @t)`(~(gas in a) ['well' 'things' 'number' 'leet' ~])
    {'number' 'things' 'leet' 'bonita' 'madeleine' 'rudolf' 'john' 'well'}
    > =s (sy `(list @t)`['a' 'b' 'c' 'd' 'e' ~])
    > `(set @t)`(~(gas in s) `(list @t)`['1' '2' '3' ~])
    {'1' '3' '2' 'e' 'd' 'a' 'c' 'b'}

------------------------------------------------------------------------

### `+-has:in`

b in a?

Checks if `b` is an element of `a`, producing a boolean.

Accepts
-------

`a` is a [`++set`]().

`b` is a [noun]().

Produces
--------

A boolean.

Source
------

      +-  has                                               ::  b exists in a check
        ~/  %has
        |*  b/*
        |-  ^-  ?
        ?~  a
          |
        ?:  =(b n.a)
          &
        ?:  (hor b n.a)
          $(a l.a)
        $(a r.a)
      ::


Examples
--------

    > =a (~(gas in `(set @t)`~) `(list @t)`['a' 'b' 'c' ~])
    > (~(has in a) 'a')
    %.y
    > (~(has in a) 'z')
    %.n

------------------------------------------------------------------------

### `+-int:in`

Intersection

Produces a set of the intersection between two `++set`s of the same type,
`a` and `b`. The intersection of two sets is composed of only elements
that appear in both of those sets.

Accepts
-------

`a` is a set.

`b` is a set.

Produces
--------

A `++set`.

Source
------

      +-  int                                               ::  intersection
        ~/  %int
        |*  b/_a
        |-  ^+  a
        ?~  b
          ~
        ?~  a
          ~
        ?.  (vor n.a n.b)
          $(a b, b a)
        ?:  =(n.b n.a)
          [n.a $(a l.a, b l.b) $(a r.a, b r.b)]
        ?:  (hor n.b n.a)
          %-  uni(a $(a l.a, b [n.b l.b ~]))  $(b r.b)
        %-  uni(a $(a r.a, b [n.b ~ r.b]))  $(b l.b)
      ::



Examples
--------

    > (~(int in `(set @t)`(sy "ac")) (sy "ha"))            ::  `++sy` turns a noun into a set
    [n='a' l={} r={}]
    > `(set @t)`(~(int in `(set @t)`(sy "ac")) (sy "ha"))
    {a}
    > (~(int in (sy "acmo")) ~)
    ~
    > (~(int in `(set @t)`(sy "acmo")) (sy "ham"))
    [n='m' l={'a'} r={}]
    > `(set @t)`(~(int in `(set @t)`(sy "acmo")) (sy "ham"))
    {'a' 'm'}
    > (~(int in `(set @t)`(sy "acmo")) (sy "lep"))
    ~
    > `(set @t)`(~(int in `(set @t)`(sy "acmo")) (sy "lep"))
    {}
------------------------------------------------------------------------

### `+-put:in`

Put b in a

Add an element `b` to the `++set` `a`.

Accepts
-------

`a` is a set.

`b` is a noun.

Produces
--------

A `++set`.

Source
------

      +-  put                                               ::  puts b in a, sorted
        ~/  %put
        |*  b/*
        |-  ^+  a
        ?~  a
          [b ~ ~]
        ?:  =(b n.a)
          a
        ?:  (hor b n.a)
          =+  c=$(a l.a)
          ?>  ?=(^ c)
          ?:  (vor n.a n.c)
            [n.a c r.a]
          [n.c l.c [n.a r.c r.a]]
        =+  c=$(a r.a)
        ?>  ?=(^ c)
        ?:  (vor n.a n.c)
          [n.a l.a c]
        [n.c [n.a l.a l.c] r.c]
      ::


Examples
--------

    > =a (~(gas in `(set @t)`~) `(list @t)`['a' 'b' 'c' ~])
    > =b (~(put in a) 'd')
    > b
    [n='b' l=[n='d' l={} r={'a' 'c'}] r=~]
    > `(set @t)`b
    {'d' 'a' 'c' 'b'}
    > -.l.+.b
    n='d'

------------------------------------------------------------------------

### `+-rep:in`

Accumulate

Accumulate the elements of `a` using a gate `b`. That is, we get a
noun produced from the elements after an operation is performed on
them by the specified gate.

Accepts
--------

`a` is a `++set`.

`b` is a gate.

Produces
--------

A noun.

Source
------

    +-  rep                                               ::  replace by product
      |*  b/_|=([* *] +<+)
      |-
      ?~  a  +<+.b
      $(a r.a, +<+.b $(a l.a, +<+.b (b n.a +<+.b)))



Examples
--------

    > =a (~(gas in *(set @)) [1 2 3 ~])
    >`(set @)`a
    {1 3 2}
    > (~(rep in a) |=([a/@ b/@] (add a b)))
    b=6

------------------------------------------------------------------------

### `+-run:in`

Apply gate to set (often called *map*)

Produce a `++set` containing the products of gate `b` applied to each element
in `a`.

Accepts
-------

`a` is a `++set`.

`b` is a gate.

Produces
--------

A set.

Source
------

    +-  run                                                ::  apply gate to values
        ~/  %run
        |*  b/gate
        =|  c/(set _?>(?=(^ a) (b n.a)))
        |-  ?~  a  c
        =.  c  (~(put in c) (b n.a))
        =.  c  $(a l.a, c c)
        $(a r.a, c c)



Examples
--------

    > =s (sy ~["a" "A" "b" "c"])                           ::  `++sy` turns a noun into a set
    > s
    [n=[i='A' t=""] l={} r={[i='a' t=""] [i='c' t=""] [i='b' t=""]}]
    > (~(run in s) cuss)
    {"A" "C" "B"}

------------------------------------------------------------------------

### `+-tap:in`

Set to list

Flattens the `++set` `a` into a `++list`.

Accepts
-------

`a` is an set.

Produces
--------

A list.

Source
------

      +-  tap                                               ::  convert to list
        ~/  %tap
        |=  b/(list _?>(?=(^ a) n.a))
        ^+  b
        ?~  a
          b
        $(a r.a b [n.a $(a l.a)])
      ::



Examples
--------

    > =s (sy `(list @t)`['a' 'b' 'c' 'd' 'e' ~])           ::  `++sy` turns a noun into a set
    > s
    [n='e' l={} r={'d' 'a' 'c' 'b'}]
    > `(set @t)`s
    {'e' 'd' 'a' 'c' 'b'}
    > ~(tap in s)
    ~['b' 'c' 'a' 'd' 'e']

------------------------------------------------------------------------

### `+-uni:in`

Union

Produces a set of the union between two [`++set`]()s of the same type, `a` and
`b`.

Accepts
-------

`a` is a set.

`b` is a set.

Produces
--------

A set.

Source
------

      +-  uni                                               ::  union
        ~/  %uni
        |*  b/_a
        ?:  =(a b)  a
        |-  ^+  a
        ?~  b
          a
        ?~  a
          b
        ?:  (vor n.a n.b)
          ?:  =(n.b n.a)
            [n.b $(a l.a, b l.b) $(a r.a, b r.b)]
          ?:  (hor n.b n.a)
            $(a [n.a $(a l.a, b [n.b l.b ~]) r.a], b r.b)
          $(a [n.a l.a $(a r.a, b [n.b ~ r.b])], b l.b)
        ?:  =(n.a n.b)
          [n.b $(b l.b, a l.a) $(b r.b, a r.a)]
        ?:  (hor n.a n.b)
          $(b [n.b $(b l.b, a [n.a l.a ~]) r.b], a r.a)
        $(b [n.b l.b $(b r.b, a [n.a ~ r.a])], a l.a)
      ::


Examples
--------
    > =s (sy ['a' 'b' 'c' 'd' 'e' ~])                      :: `++sy` turns a noun into a set
    > =t (sy ['v' 'g' 'a' ~])
    > (~(uni in t) s)
    [n='e' l=~ r=[n='b' l={'d' 'g' 'a' 'c'} r={'v'}]]
    > `(set @t)`(~(uni in t) s)
    {'e' 'd' 'g' 'a' 'c' 'b' 'v'}
    > `(set @)`(~(uni in (sy 1 2 3 ~)) (sy 3 4 5 ~))
    {5 4 1 3 2}
    > `(set @t)`(~(uni in (sy ['a' 'c' 'm' 'o' ~])) (sy ['h' 'a' 'm' ~]))
    {'a' 'c' 'm' 'o' 'h'}
    > `(set @t)`(~(uni in (sy ['a' 'c' 'm' 'o' ~])) (sy ['l' 'e' 'p' ~]))
    {'e' 'a' 'c' 'm' 'l' 'o' 'p'}

------------------------------------------------------------------------

### `+-wyt:in`

Set size

Produce the number of elements in [`++set`]() `a` as an atom.

Accepts
-------

`a` is an set.

Produces
--------

An atom.

Source
------

      +-  wyt                                               ::  size of set
        |-  ^-  @
        ?~(a 0 +((add $(a l.a) $(a r.a))))
      --



Examples
--------

    > =a (~(put in (~(put in (sy)) 'a')) 'b')              ::  `++sy` turns a noun into a set
    > ~(wyt in a)
    2
    > =b (sy ['bonita' 'madeleine' 'daniel' 'john' ~])
    > ~(wyt in b)
    4


***
